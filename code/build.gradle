plugins {
    id 'application'
    id 'jacoco'
    id 'com.diffplug.spotless' version '8.1.0'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation libs.junit.jupiter

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation libs.guava
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jacoco {
    toolVersion = "0.8.8"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation"
}

tasks.named('test') {
    testClassesDirs = files(testClassesDirs.files.sort())

    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }

    afterTest { desc, result ->
        println "Test ${desc.className}.${desc.name} took ${result.endTime - result.startTime} ms"
    }
}

tasks.jacocoTestReport {
    reports {
        xml.required = true
        csv.required = true
        html.required = true
    }
}

spotless {
    java {
        target 'src/**/*.java'

        googleJavaFormat('1.22.0')
                .reflowLongStrings()
                .groupArtifact('com.google.googlejavaformat:google-java-format')
                .aosp()

        removeUnusedImports()
        importOrder 'java', 'javax', 'org', 'com', ''

        trimTrailingWhitespace()
        endWithNewline()
    }

    groovy {
        target 'src/**/*.groovy'
        greclipse()
        trimTrailingWhitespace()
        endWithNewline()
    }

    format 'misc', {
        target '*.md', '.gitignore', '.editorconfig'
        trimTrailingWhitespace()
        endWithNewline()
    }
}
